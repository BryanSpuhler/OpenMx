

! SCRIPT NAME	: rawVCmod1.mx  (sp)
! GOAL		:  GxE - Basic model: To model continuous moderation effects on variance components
! DATA		: continuous
! INPUT		: raw data
! UNI/BI/MULTI	: uni
! DATA-GROUPS	: MZ DZ 
! MEANS MODEL	: grand mean, moderator effect
! VARIANCE COVARIANCE MODEL(S)	: ACE +  moderator-linked A component,  C component, E component 
!
!

! Downloading Mx software: http://www.vcu.edu/mx
! Mx script's library: http://www.psy.vu.nl/mxbib
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! GxE - Basic model
! S. Purcell (s.purcell@iop.kcl.ac.uk)

G1: Define Matrices
Data Calc NGroups=3
Begin Matrices;
A full 1 1 free
C full 1 1 free
E full 1 1 free
T full 1 1 free	! moderator-linked A component
U full 1 1 free	! moderator-linked C component
V full 1 1 free	! moderator-linked E component
M full 1 1 free	! grand mean
B full 1 1 fixed ! moderator-linked means model
H full 1 1
R full 1 1	! twin 1 moderator (definition variable)
S full 1 1	! twin 2 moderator (definition variable)
End Matrices;
Ma T 0
Ma U 0
Ma V 0
Ma M 0
Ma B 0
Ma A 1
Ma C 1
Ma E 1
Matrix H .5
Options NO_Output
End

G2: MZ
Data NInput_vars=12 NObservations=0
!Missing =NA
RE File=myTwinData.txt2
Labels fam age zyg part wt1 wt2 ht1 ht2 htwt1 htwt2 bmi1 bmi2
Select if zyg = 1 /
Select bmi1 bmi2 age /
Definition age /
Matrices = Group 1
Means M + B*R |M + B*S / 
Covariance 	 
(A+T*R)*(A+T*R)' + (C+U*R)*(C+U*R)' + (E+V*R)*(E+V*R)'  | (A+T*R)*(A+T*S)' +  (C+U*R)*(C+U*S)'  _
(A+T*S)*(A+T*R)' + (C+U*S)*(C+U*R)' | (A+T*S)*(A+T*S)' + (C+U*S)*(C+U*S)' + (E+V*S)*(E+V*S)' /
!twin 1 moderator variable
Specify R -1 
!twin 2 moderator variable
Specify S -1
Options NO_Output
End
 
G3: DZ
Data NInput_vars=12 NObservations=0
!Missing =-999
RE File=myTwinData.txt2
Labels fam age zyg part wt1 wt2 ht1 ht2 htwt1 htwt2 bmi1 bmi2
Select if zyg = 3 /
Select bmi1 bmi2 age /
Definition age /
Matrices = Group 1
Means M + B*R |M + B*S / 
Covariance 	 
(A+T*R)*(A+T*R)' + (C+U*R)*(C+U*R)' + (E+V*R)*(E+V*R)'  | H@(A+T*R)*(A+T*S)' +  (C+U*R)*(C+U*S)'  _
H@(A+T*S)*(A+T*R)' + (C+U*S)*(C+U*R)' | (A+T*S)*(A+T*S)' + (C+U*S)*(C+U*S)' + (E+V*S)*(E+V*S)' /
!twin 1 moderator variable
Specify R -1
!twin 2 moderator variable
Specify S -1
End

